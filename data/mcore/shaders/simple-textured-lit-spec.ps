
/* medea - an Open Source, WebGL-based 3d engine for next-generation browser games.
 * (or alternatively, for clumsy and mostly useless tech demos written solely for fun)
 *
 * medea is (c) 2011, Alexander C. Gessler
 * licensed under the terms and conditions of a 3 clause BSD license.
 */
 
uniform vec3 LIGHT_D0_DIR;

uniform sampler2D texture;
uniform float shininess;

varying vec3 eye;

#include <remote:mcore/shaders/core.psh>

// currently all lighting computation happens in worldspace
void main()
{
	vec3 N = normalize(FetchNormal());

	// diffuse contribution
	float diffuse = dot(normalize(LIGHT_D0_DIR),N);

	// specular contribution (blinn)
	vec3 E = normalize(eye);
	vec3 H = normalize(LIGHT_D0_DIR + E);
	float specular = pow( max(dot(H, N), 0.0), shininess );

	// self-shadowing
	specular *= clamp(diffuse*4.0, 0.0, 1.0);

	// final lighting term, using the half-lambert term from source 
	float lighting = min(specular + ((diffuse + 1.0) * 0.5), 1.0);

	vec3 tex = texture2D(texture, FetchTexCoord()).rgb;

    gl_FragColor.a = 1.0;
	gl_FragColor.rgb = tex * lighting * lighting;
}

